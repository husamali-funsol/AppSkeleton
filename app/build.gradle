plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
    // uncomment after adding json file from firebase
//    id 'com.google.gms.google-services'

    id 'com.google.firebase.crashlytics'
}

android {
    namespace 'com.android.ar.ruler'
    compileSdk 34

    defaultConfig {

        applicationId "com.android.ar.ruler"
        minSdk 23
        //noinspection EditedTargetSdkVersion
        targetSdk 34

        versionCode 1
        versionName "1.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        debug {

            // Debug Ids copied from Admob test IDs
            resValue "string", "admob_app_id", "ca-app-pub-3940256099942544~3347511713"
            resValue "string", "app_open_id", "ca-app-pub-3940256099942544/3419835294"
            resValue "string", "rewarded_id", "ca-app-pub-3940256099942544/5224354917"
            resValue "string", "splash_interstitial_id", "ca-app-pub-3940256099942544/1033173712"
            resValue "string", "inner_interstitial_id", "ca-app-pub-3940256099942544/1033173712"
            resValue "string", "home_native_id", "ca-app-pub-3940256099942544/2247696110"
            resValue "string", "settings_native_id", "ca-app-pub-3940256099942544/2247696110"
            resValue "string", "language_native_id", "ca-app-pub-3940256099942544/2247696110"
            resValue "string", "exit_native_id", "ca-app-pub-3940256099942544/2247696110"

            // For disable crashlytics in debug mode
            manifestPlaceholders["crashlyticsCollectionEnabled"] = false

            debuggable true
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        release {

            // All the release IDs will go here


            // For enable crashlytics in release mode
            manifestPlaceholders["crashlyticsCollectionEnabled"] = true

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    buildFeatures {
        viewBinding true
    }

    // To avoid splitting languages (by default it will split language in bundle and user will only be able to download app with language of his/her region.)
    bundle {
        language {
            enableSplit = false
        }
    }

    // For checking release build
    lintOptions {
        // This option controls whether lint runs on release builds. It's generally a good idea to run lint on release builds to catch potential issues before releasing your app.
        checkReleaseBuilds true
        // This option determines whether the build process should be aborted if any critical issues are found by the lint tool. If set to true, the build will fail if lint reports any issues marked as "error."
        abortOnError true
    }
}

    // This code snippet provide meaningful and informative APK file names. It helps you keep track of which variant was built, when it was built, and what version it corresponds to.
android.applicationVariants.all { variant ->
    variant.outputs.all {
        def appName = "YourAppNameHere"
        def versionName = variant.versionName
        def versionCode = variant.versionCode
        def formattedDate = new Date().format('dd-MM-yy HH-mm')
        def variantName = variant.name // e. g. freeDebug
        def apkName = appName + '_' + variantName + ' VCodes ' + versionCode + '  VName ' + versionName + ' _' + formattedDate + '.apk'
        outputFileName = apkName
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'

    // Navigation Component
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.0'

    // Dagger Hilt
    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-android-compiler:2.44"

    // Responsive Across Densities
    implementation 'com.intuit.sdp:sdp-android:1.1.0'
    implementation 'com.intuit.ssp:ssp-android:1.1.0'

    // Kotlin Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4'

    // Firebase Bom
    implementation platform('com.google.firebase:firebase-bom:31.2.0')

    // Firebase Crashlytics
    implementation 'com.google.firebase:firebase-crashlytics'

    // Firebase Analytics
    implementation 'com.google.firebase:firebase-analytics'

    // Firebase Messaging
    implementation 'com.google.firebase:firebase-messaging'

    // Firebase Remote Config
    implementation 'com.google.firebase:firebase-config:21.4.1'

    // Force Update
    implementation 'com.google.android.play:app-update-ktx:2.1.0'

    // In App Review
    implementation("com.google.android.play:review-ktx:2.0.1")

    // Billing
    implementation 'com.android.billingclient:billing:6.0.1'
    implementation 'com.github.Funsol-Projects:Funsol-Billing-Helper:v1.0.3'

    // Admob Ads
    implementation 'com.google.android.gms:play-services-ads:22.2.0'
    // LifecycleObserver for App Open Ads
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    annotationProcessor "androidx.lifecycle:lifecycle-compiler:2.6.1"

}